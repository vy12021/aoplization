apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.jjohannes.extra-java-module-info'

java {
  sourceCompatibility = rootProject.ext.sourceCompatibilityVersion
  targetCompatibility = rootProject.ext.targetCompatibilityVersion
}

extraJavaModuleInfo {
  failOnMissingModuleInfo.set(false)
  module('auto-common-1.2.1.jar', 'com.google.auto.common', '1.2.1') {
    exports('com.google.auto.common')
    requires('jdk.compiler')
  }
  module('incap-0.3.jar', 'net.ltgt.gradle.incap', '0.3') {
    exports('net.ltgt.gradle.incap')
  }
  module('incap-processor-0.3.jar', 'net.ltgt.gradle.incap.processor', '0.3')
}

compileJava {
  options.compilerArgs = [
          '--add-exports', 'jdk.compiler/com.sun.tools.javac.code=componentization.compiler',
          '--add-opens', 'jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED',
          '--add-exports', 'jdk.compiler/com.sun.tools.javac.util=componentization.compiler',
          '--add-opens', 'jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED',
  ]
}

dependencies {
  implementation project(':annotation')
  implementation 'com.squareup:javapoet:1.13.0'
  implementation 'com.google.auto:auto-common:1.2.1'
  implementation 'org.jetbrains:annotations:23.0.0'
  compileOnly 'com.google.auto.service:auto-service:1.0.1'
  kapt 'com.google.auto.service:auto-service:1.0.1'

  implementation 'net.ltgt.gradle.incap:incap:0.3'
  kapt 'net.ltgt.gradle.incap:incap-processor:0.3'
}


apply from: "${rootProject.file('mvn-push.gradle')}"
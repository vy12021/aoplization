apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.jjohannes.extra-java-module-info'

java {
  sourceCompatibility = rootProject.ext.sourceCompatibilityVersion
  targetCompatibility = rootProject.ext.targetCompatibilityVersion
}

extraJavaModuleInfo {
  failOnMissingModuleInfo.set(false)
  module("kotlinpoet-${kotlinpoet_ksp_version}.jar", 'com.squareup.kotlinpoet', kotlinpoet_ksp_version) {
    requires('kotlin.stdlib')
  }
  module("kotlinpoet-ksp-${kotlinpoet_ksp_version}.jar", 'com.squareup.kotlinpoet.ksp', kotlinpoet_ksp_version) {
    requires('kotlin.stdlib')
  }
  module("symbol-processing-api-${kotlin_version}-${ksp_version}.jar", 'symbol.processing.api', "${kotlin_version}-${ksp_version}") {
    requires('kotlin.stdlib')
  }
}

compileJava {
  options.compilerArgs = [
          '--add-exports', 'jdk.compiler/com.sun.tools.javac.code=aoplization.compiler',
          '--add-opens', 'jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED',
          '--add-exports', 'jdk.compiler/com.sun.tools.javac.util=aoplization.compiler',
          '--add-opens', 'jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED',
  ]
}

dependencies {
  implementation project(':annotation')
  implementation "com.squareup:javapoet:${javapoet_version}"
  implementation "com.squareup:kotlinpoet-ksp:${kotlinpoet_ksp_version}"
  implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}"
  implementation "com.google.devtools.ksp:symbol-processing-api:${kotlin_version}-${ksp_version}"

}


apply from: "${rootProject.file('mvn-push.gradle')}"